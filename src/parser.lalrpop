use std::str::FromStr;
use std::rc::Rc;
use std::collections::HashMap;
use std::convert::TryInto;
use lalrpop_util::ParseError;
use crate::ast::*;
use crate::lexer;

grammar;

pub Top : Program = {
    NL* <TopLevelExpr*> => Program{ exprs: <> },
};

#[inline]
Comma<T>: Vec<T> = {
    <items: (<T> ",")*> <last: T?> => {
        let mut items = items;
        items.extend(last);
        items
    }
};

// Expressions
Expr = <StandaloneExpr> NL+;
TopLevelExpr = {
    <DefStatement> NL+,
    <Expr>,
};
StandaloneExpr = ReturnExpr;

// Function definitions
DefStatement: NodeBox = {
    "def" <id:Identifier> <args:DefArgumentList?> NL+ <exprs:Expr*> "end" => {
        NodeBox::new(DefStatement {
            id: Rc::from(id),
            args: args.unwrap_or_else(|| vec![]),
            exprs
        })
    },
};

DefArgumentList: Vec<(Rc<str>, Option<NodeBox>)> = {
    "(" <args:Comma<DefArgument>> ")" => args
};

DefArgument: (Rc<str>, Option<NodeBox>) = {
    <id:Identifier> => {
        (Rc::from(id), None)
    },
    <id:Identifier> ":" <ty:Type> => {
        (Rc::from(id), Some(ty))
    },
};

// Return expression
ReturnExpr: NodeBox = {
    "return" <expr:StandaloneExpr> => {
        NodeBox::new(ReturnExpr { expr })
    },
    LetExpr,
};

// Let expression
LetExpr: NodeBox = {
    "let" <left:Term> "=" <right:StandaloneExpr> => {
        NodeBox::new(LetExpr{ left, right })
    },
    BinExpr,
};

// Binary expressions
BinOpTier<Op,NextTier>: NodeBox = {
    <left:BinOpTier<Op,NextTier>> <op:Op> <right:NextTier> => NodeBox::new(BinExpr{ left, right, op }),
    NextTier
};

BinExpr = AsgExpr;
AsgExpr = BinOpTier<AsgOp, AndExpr>;
AndExpr = BinOpTier<AndOp, OrzExpr>;
OrzExpr = BinOpTier<OrzOp, CmpExpr>;
CmpExpr = BinOpTier<CmpOp, AddExpr>;
AddExpr = BinOpTier<AddOp, FacExpr>;
FacExpr = BinOpTier<FacOp, IfExpr>;

AsgOp: BinOp = {
    "=" => BinOp::Asg,
    "+=" => BinOp::Adds,
    "-=" => BinOp::Subs,
    "*=" => BinOp::Muls,
    "/=" => BinOp::Divs,
};

AndOp: BinOp = {
    "and" => BinOp::And,
};

OrzOp: BinOp = {
    "or" => BinOp::Or,
};

CmpOp: BinOp = {
    "==" => BinOp::Equ,
    "!=" => BinOp::Neq,
};

AddOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
};

FacOp: BinOp = {
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
};

// If expression
IfExpr: NodeBox = {
    "if" <cond:StandaloneExpr> NL+ <exprs:Expr*> "else" NL+ <elses:Expr*> "end" => {
        NodeBox::new(IfExpr::new( cond, exprs, elses ))
    },
    "if" <cond:StandaloneExpr> NL+ <exprs:Expr*> "end" => {
        NodeBox::new(IfExpr::new( cond, exprs, vec![] ))
    },
    CallExpr,
};

// Call expression
CallExpr: NodeBox = {
    <callee:Term> "(" <args:Comma<StandaloneExpr>> ")" => {
        NodeBox::new(CallExpr::new(callee, args))
    },
    Term,
};

// Literals / terms
Term: NodeBox = {
    <Literal> => NodeBox::new(<>),
    "(" <Expr> ")" => <>,
};

Literal: Value = {
    <Int> => Value::Int(<>.try_into().unwrap()),
    <String> => Value::String(Rc::from(<>)),
    <Identifier> => Value::Identifier(Rc::from(<>)),
};

// Types
Type: NodeBox = {
    <Identifier> => NodeBox::new(TypeId::Identifier(Rc::from(<>))),
};


// Hook external lexer:
extern {
    type Location = usize;
    type Error = lexer::Error;

    enum lexer::Tok {
        "def" => lexer::Tok::Def,
        "return" => lexer::Tok::Return,
        "let" => lexer::Tok::Let,
        "if" => lexer::Tok::If,
        "else" => lexer::Tok::Else,
        "end" => lexer::Tok::End,
        "and" => lexer::Tok::And,
        "or" => lexer::Tok::Or,
        "==" => lexer::Tok::Equ,
        "!=" => lexer::Tok::Neq,
        "+" => lexer::Tok::Add,
        "-" => lexer::Tok::Sub,
        "*" => lexer::Tok::Mul,
        "/" => lexer::Tok::Div,
        "=" => lexer::Tok::Asg,
        "+=" => lexer::Tok::Adds,
        "-=" => lexer::Tok::Subs,
        "*=" => lexer::Tok::Muls,
        "/=" => lexer::Tok::Divs,
        "(" => lexer::Tok::LeftParen,
        ")" => lexer::Tok::RightParen,
        "," => lexer::Tok::Comma,
        ":" => lexer::Tok::Colon,
        NL => lexer::Tok::Newline,
        Int => lexer::Tok::Int { value : <i64> },
        Identifier => lexer::Tok::Identifier { value : <String> },
        String => lexer::Tok::String { value : <String> },
    }
}