use std::rc::Rc;
use std::cell::RefCell;
use crate::ssa::isa::IntrinsicType;
use crate::ast::*;
use crate::lexer;

grammar;

pub Top : Program = {
    NL* <TopLevelExpr*> EOF? => Program{ exprs: <> },
};

#[inline]
Comma<T>: Vec<T> = {
    <items: (<T> ",")*> <last: T?> => {
        let mut items = items;
        items.extend(last);
        items
    }
};

// Expressions
NLOrEOF: () = {
    NL+ => (),
    EOF => (),
};
Expr = <StandaloneExpr> NLOrEOF;
TopLevelExpr = {
    <ImportStatement> NLOrEOF,
    <DefStatement> NLOrEOF,
    <ExternDefStatement> NLOrEOF,
    <Expr>,
};
StandaloneExpr = ReturnExpr;

// Attributes
AttributeList: Vec<AttributeValue> = {
    <items:(<AttributeValue> NL)+> => {
        items
    }
}

AttributeValue: AttributeValue = {
    "@[" <name:Identifier> "]" => {
        AttributeValue {
            name,
            args: vec![],
        }
    },
    "@[" <name:Identifier> "(" <args:Comma<Identifier>> ")" "]" => {
        AttributeValue {
            name,
            args,
        }
    },
}

// Function definitions
ImportStatement: NodeBox = {
    "import" <path:String> => {
        NodeBox::new(ImportStatement {
            path
        })
    }
};

DefStatement: NodeBox = {
    <attrs:AttributeList?> "def"
        <id:Identifier>
        <args:DefArgumentList?>
        <return_type:TypeSpecifier?>
    NL+ <exprs:Expr*> "end" => {
        NodeBox::new(DefStatement {
            id: Rc::from(id),
            args: args.unwrap_or_else(|| vec![]),
            exprs,
            return_type,
            attrs,
            intrinsic: RefCell::new(IntrinsicType::None)
        })
    },
};

DefArgumentList: Vec<(Rc<str>, Option<TypeId>)> = {
    "(" <args:Comma<DefArgument>> ")" => args
};

DefArgument: (Rc<str>, Option<TypeId>) = {
    <id:Identifier> <type_specifier:TypeSpecifier?> => {
        (Rc::from(id), type_specifier)
    },
};

ExternDefStatement: NodeBox = {
    "extern" "def"
        <id:Identifier>
        <symbol:ExternSymbol?>
        <args:DefArgumentList?>
        <return_type:TypeSpecifier?> => {
        NodeBox::new(DefStatement {
            id: Rc::from(id.clone()),
            args: args.unwrap(),
            exprs: vec![],
            return_type: Some(return_type.unwrap()),
            attrs: None,
            intrinsic: RefCell::new(IntrinsicType::Extern(symbol.unwrap_or(id)))
        })
    },
};

ExternSymbol: String = "=" <String>;

// Return expression
ReturnExpr: NodeBox = {
    "return" <expr:StandaloneExpr> => {
        NodeBox::new(ReturnExpr { expr })
    },
    LetExpr,
};

// Let expression
LetExpr: NodeBox = {
    <left:Term> ":" "=" <right:StandaloneExpr> => {
        NodeBox::new(LetExpr{ left, right })
    },
    BinExpr,
};

// Binary expressions
BinOpTier<Op,NextTier>: NodeBox = {
    <left:BinOpTier<Op,NextTier>> <op:Op> <right:NextTier> => NodeBox::new(BinExpr{ left, right, op }),
    NextTier
};

BinExpr = AsgExpr;
AsgExpr = BinOpTier<AsgOp, AndExpr>;
AndExpr = BinOpTier<AndOp, OrzExpr>;
OrzExpr = BinOpTier<OrzOp, CmpExpr>;
CmpExpr = BinOpTier<CmpOp, AddExpr>;
AddExpr = BinOpTier<AddOp, FacExpr>;
FacExpr = BinOpTier<FacOp, AsExpr>;

AsgOp: BinOp = {
    "=" => BinOp::Asg,
    "+=" => BinOp::Adds,
    "-=" => BinOp::Subs,
    "*=" => BinOp::Muls,
    "/=" => BinOp::Divs,
    "%=" => BinOp::Mods,
};

AndOp: BinOp = {
    "and" => BinOp::And,
};

OrzOp: BinOp = {
    "or" => BinOp::Or,
};

CmpOp: BinOp = {
    "==" => BinOp::Equ,
    "!=" => BinOp::Neq,
    "<"  => BinOp::Lt,
    ">"  => BinOp::Gt,
    "<=" => BinOp::Lte,
    ">=" => BinOp::Gte,
};

AddOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
};

FacOp: BinOp = {
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
    "%" => BinOp::Mod,
};

// As cast expression
AsExpr: NodeBox = {
    <left:WhileExpr> "as" <typed:Type> => {
        NodeBox::new(AsExpr {
            left,
            typed
        })
    },
    WhileExpr,
};

// While expression
WhileExpr: NodeBox = {
    "while" <cond:StandaloneExpr> NL+ <exprs:Expr*> "end" => {
        NodeBox::new(WhileExpr { cond, exprs })
    },
    IfExpr
};

// If expression
IfExpr: NodeBox = {
    "if" <cond:StandaloneExpr> NL+ <exprs:Expr*> <elsif:IfExprElsif*> <elses:IfExprElse?> "end" => {
        if !elsif.is_empty() {
            let last_elsif = elsif.last().unwrap();
            let mut elsif_expr = NodeBox::new(IfExpr {
                cond: last_elsif.0.clone(),
                exprs: last_elsif.1.clone(),
                elses: elses.unwrap_or_else(|| vec![])
            });
            for it_elsif in elsif.iter().rev().skip(1) {
                elsif_expr = NodeBox::new(IfExpr {
                    cond: it_elsif.0.clone(),
                    exprs: it_elsif.1.clone(),
                    elses: vec![ elsif_expr ],
                });
            }
            return NodeBox::new(IfExpr { cond, exprs, elses: vec![ elsif_expr ] });
        }
        NodeBox::new(IfExpr { cond, exprs, elses: elses.unwrap_or_else(|| vec![]) })
    },
    CallExpr,
};

IfExprElsif: (NodeBox, Vec<NodeBox>) = {
    "elsif" <cond:StandaloneExpr> NL+ <exprs:Expr*> => {
        (cond, exprs)
    },
};
IfExprElse: Vec<NodeBox> = "else" NL+ <Expr*>;

// Call expression
CallExpr: NodeBox = {
    <callee:MemberExpr> "(" <args:Comma<StandaloneExpr>> ")" => {
        NodeBox::new(CallExpr::new(callee, args))
    },
    MemberExpr,
};

// Member expressions
MemberExpr: NodeBox = {
    <first:Term> <rest: MemberExprArm+> => {
        let mut memexpr = NodeBox::new(MemberExpr {
            left: first,
            right: rest[0].clone(),
        });
        for right in rest.iter().skip(1) {
            memexpr = NodeBox::new(MemberExpr {
                left: memexpr,
                right: right.clone(),
            });
        }
        memexpr
    },
    Term,
};

MemberExprArm: MemberExprArm = {
    "." <id:Identifier> => MemberExprArm::Identifier(Rc::from(id)),
    "[" <expr:StandaloneExpr> "]" => MemberExprArm::Index(expr),
};

// Literals / terms
Term: NodeBox = {
    <Literal> => NodeBox::new(<>),
    "(" <StandaloneExpr> ")" => <>,
};

Literal: Value = {
    <I32> => Value::I32(<>),
    <I64> => Value::I64(<>),
    <Float> => Value::Float(<>),
    <String> => Value::String(Rc::from(<>)),
    <Identifier> => Value::Identifier(Rc::from(<>)),
};

// Types
Type: TypeId = {
    <Identifier> => TypeId::new(TypeIdData::Identifier(Rc::from(<>))),
};

TypeSpecifier: TypeId = {
    ":" <Type> => <>,
};


// Hook external lexer:
extern {
    type Location = usize;
    type Error = lexer::Error;

    enum lexer::Tok {
        "import" => lexer::Tok::Import,
        "def" => lexer::Tok::Def,
        "extern" => lexer::Tok::Extern,
        "return" => lexer::Tok::Return,
        "if" => lexer::Tok::If,
        "while" => lexer::Tok::While,
        "else" => lexer::Tok::Else,
        "elsif" => lexer::Tok::Elsif,
        "end" => lexer::Tok::End,
        "and" => lexer::Tok::And,
        "or" => lexer::Tok::Or,
        "as" => lexer::Tok::As,
        "==" => lexer::Tok::Equ,
        "!=" => lexer::Tok::Neq,
        "+" => lexer::Tok::Add,
        "-" => lexer::Tok::Sub,
        "*" => lexer::Tok::Mul,
        "/" => lexer::Tok::Div,
        "%" => lexer::Tok::Mod,
        "=" => lexer::Tok::Asg,
        "+=" => lexer::Tok::Adds,
        "-=" => lexer::Tok::Subs,
        "*=" => lexer::Tok::Muls,
        "/=" => lexer::Tok::Divs,
        "%=" => lexer::Tok::Mods,
        "<" => lexer::Tok::Lt,
        ">" => lexer::Tok::Gt,
        "<=" => lexer::Tok::Lte,
        ">=" => lexer::Tok::Gte,
        "(" => lexer::Tok::LeftParen,
        ")" => lexer::Tok::RightParen,
        "," => lexer::Tok::Comma,
        "." => lexer::Tok::Dot,
        ":" => lexer::Tok::Colon,
        "@" => lexer::Tok::At,
        "@[" => lexer::Tok::AtBracket,
        "[" => lexer::Tok::LeftBracket,
        "]" => lexer::Tok::RightBracket,
        NL => lexer::Tok::Newline,
        EOF => lexer::Tok::EOF,
        I32 => lexer::Tok::I32 { value : <i32> },
        I64 => lexer::Tok::I64 { value : <i64> },
        Float => lexer::Tok::Float { value : <u64> },
        Identifier => lexer::Tok::Identifier { value : <String> },
        String => lexer::Tok::String { value : <String> },
    }
}